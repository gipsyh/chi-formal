#lang ivy1.7

include basic
include message

module txn_slot_t = {
    individual in_use: bool
    individual addr: addr_t
    individual wait: bool
    individual wake: txn_idx_t
    individual handled: bool
    individual msg: midx_t

    after init {
        in_use := false;
    }

    action default = {
        wait := true;
        handled := false;
        wake := txn_idx_null;
    }
}

module channel_t = {
    individual head: midx_t
    individual tail: midx_t

    after init {
        head := midx_null;
        tail := midx_null;
    }

    action send returns(m: midx_t) = {
        m := msg_new;
        if tail = midx_null {
            assert head = midx_null;
            tail := m;
            head := m;
        } else {
            assert msg(tail).list_next = midx_null;
            msg(tail).list_next := m;
            tail := m;
        }
    }

    action recv returns (m: midx_t) = {
        assume head ~= midx_null;
        m := head;
        if head = tail {
            head := midx_null;
            tail := midx_null;
        } else {
            head := msg(head).list_next;
        }
    }
}

module node_t = {
    individual cache_state(A: addr_t): cache_state_t
    individual cache_data(A: addr_t): data_t

    derived cs_is_u(A: addr_t) = cache_state(A) = cs_ud | cache_state(A) = cs_uc
    derived cs_is_c(A: addr_t) = cache_state(A) = cs_sc | cache_state(A) = cs_uc
    derived cs_is_d(A: addr_t) = cache_state(A) = cs_ud
    derived cs_is_i(A: addr_t) = cache_state(A) = cs_i

    after init {
        cache_state(A) := cs_i;
    }

    instance req_channel: channel_t
    instance data_channel: channel_t
    instance resp_channel: channel_t
    instance snp_channel: channel_t

    instance txns(I: txn_idx_t): txn_slot_t

    action txn_new returns(txn_r: txn_idx_t) = {
        assume txn_r~= txn_idx_null & ~txns(txn_r).in_use;
        txns(txn_r).default;
        txns(txn_r).in_use := true;
    }

    action txn_free (f: txn_idx_t) = {
        assert txns(f).in_use;
        txns(f).in_use := false;
    }

    individual addr_wait_list(A: addr_t): txn_idx_t

    after init {
        addr_wait_list(A) := txn_idx_null;
    }

    derived addr_is_wait(A: addr_t) = addr_wait_list(A) ~= txn_idx_null
    
    action txn_set_wait(addr: addr_t, txn: txn_idx_t) = {
        var ptxn := addr_wait_list(addr);
        if ptxn ~= txn_idx_null {
            txns(ptxn).wake := txn;
        }
        txns(txn).wait := true;
        addr_wait_list(addr) := txn;
    }

    action txns_unset_wait(txn: txn_idx_t) = {
        txns(txn).wait := false;
    }

    action txns_hanlded(txn: txn_idx_t) = {
        txns(txn).wait := true;
    }

    action txns_finished(txn: txn_idx_t) = {
        var wake: txn_idx_t := txns(txn).wake;
        assert txns(txn).in_use;
        if wake ~= txn_idx_null {
            txns_unset_wait(wake);
        } else {
            var addr: addr_t := msg(txns(txn).msg).addr;
            assert addr_wait_list(addr) = txn;
            addr_wait_list(addr) := txn_idx_null;
        }
        txns(txn).in_use := false;
    }
}

instance nodes(N: nid_t): node_t

invariant nodes(hn_nid).cs_is_i(A)
invariant nodes(sn_nid).cs_is_i(A)
